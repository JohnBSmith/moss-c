

sub scan_error(s,i,info)
  s=s+"\n"+"\s"*i+"^"
  object(scan_error, {value=info, s=s})
end

sub scan(s)
  a=[]
  i=0; n=size(s)
  while i<n
    c=s[i]
    if c=='='
      if i+1<n and s[i+1]=='='
        a.push(["op","==",i])
        i+=2
      else
        a.push(["op","!=",i])
        i++
      end
    elif c in "<>"
      if i+1<n and s[i+1]=='='
        a.push(["op",s[i]+"=",i])
        i+=2
      else
        a.push(["op",c,i])
        i++
      end
    elif c in "+-*%/^"
      if i==0 or a[-1][0] in ["bleft", "sep", "op"]
        a.push(["op","u"+c,i])
      else
        a.push(["op",c,i])
      end
      i++
    elif c.isalpha()
      j=i
      while i<n and s[i].isalnum() do i++
      end
      a.push(["id",s[j..i-1],j])
    elif c.isdigit()
      j=i
      while i<n and s[i].isdigit() do i++
      end
      a.push(["int",s[j..i-1],j])
    elif c in "([{"
      a.push(["bleft",c,i])
      i++
    elif c in ")]}"
      a.push(["bright",c,i])
      i++
    elif c in ",;"
      a.push(["sep",c,i])
      i++
    else
      raise scan_error(s,i,"Syntax error: unexpected character: "+c+".")
    end
  end
  return a
end

sub main
  while true
    s = input("> ")
    try
      a = scan(s)
      print(a)
    catch e: scan_error
      print(e.s,"\n",e.value)
    end
  end
end

main()


