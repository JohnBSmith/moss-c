
use nt: gcd

function fraction(n,d)
  t = gcd(n,d)
  return table Fraction{n=n//t, d=d//t}
end

Fraction = table{
  function string
    [self.n, "/", self.d].join()
  end,
  function add(b)
    if b is in Int
      n = self.n+b*self.d
      d = self.d
      return fraction(n,d)
    else
      n = self.n*b.d+b.n*self.d
      d = self.d*b.d
      return fraction(n,d)
    end
  end,
  function radd(b)
    return fraction(self*b.d+b.n,b.d)
  end,
  function sub(b)
    if b is in Int
      n = self.n-b*self.d
      d = self.d
      return fraction(n,d)
    else
      n = self.n*b.d-b.n*self.d
      d = self.d*b.d
      return fraction(n,d)
    end
  end,
  function rsub(b)
    return fraction(self*b.d-b.n,b.d)
  end,
  function mul(b)
    if b is in Int
      n = self.n*b
      d = self.d
      return fraction(n,d)
    else
      n = self.n*b.n
      d = self.d*b.d
      return fraction(n,d)
    end
  end,
  function rmul(b)
    return fraction(self*b.n,b.d)
  end,
  function div(b)
    if b is in Int
      n = self.n
      d = self.d*b
      return fraction(n,d)
    else
      n = self.n*b.d
      d = self.d*b.n
      return fraction(n,d)
    end
  end,
  function rdiv(b)
    return fraction(self*b.d,b.n)
  end,
  function pow(b)
    if b>0
      return fraction(self.n^b, self.d^b)
    elif b<0
      return fraction(self.d^(-b), self.n^(-b))
    else
      return fraction(1,1)
    end
  end
}

u = fraction(1,1)


