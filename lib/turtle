
import math: pi, cos, sin
import gx, time
tau = 2*pi

new = sub|w,h|
  canvas = gx.new(w,h,1)
  canvas.clear(1,1,1)
  canvas.mix(0)
  canvas.cset(0,0,0.6)
  return table[turtle_type]
    x=0, y=0, direction=0, pen_down=true, canvas=canvas, a=[]
  end
end

turtle_type = table
  sub turn(alpha)
    self.direction = (self.direction+alpha)%1.0
  end, 
  sub move(d)
    x1=self.x; y1=self.y
    direction=self.direction*tau
    x2=x1+d*cos(direction)
    y2=y1+d*sin(direction)
    if self.pen_down
      self.canvas.line(x1,y1,x2,y2)
    end
    self.x=x2; self.y=y2
  end,
  sub pen(n)
    self.pen_down = n==0? false: true
  end,
  sub idle
    c = self.canvas
    c.flush()
    while c.key()!="q"
      time.sleep(0.1)
    end
  end,
  sub push
    self.a.push([self.x,self.y,self.direction])
  end,
  sub pop
    t=self.a.pop()
    self.x=t[0]
    self.y=t[1]
    self.direction=t[2]
  end
end


