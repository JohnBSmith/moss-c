
extend(Map,table{
  function mul(b)
    set(list(self)*list(b))
  end,
  function add(b)
    set(self.map(|x| [0,x])) | set(b.map(|x| [1,x])) 
  end,
  function pow(n)
    if type(n) is int.type
      return set(list(self)^n)
    else
      a = list(n)
      c = list(self)^size(a)
      return set(c.map(|t| (0..size(t)-1).dict(|i| [a[i],t[i]])))
    end
  end,
  function rpow(a)
    if self==2
      return a.reduce({{}},|p,x| p|set(p.map(|s| s|{x})))
    else
      return set(self)^a
    end
  end,
  function div(p)
    if p.argc()==2
      return set((list(self)/p).map(set))
    end
    d={}
    for x in self
      y = p(x)
      if y in d
        d[y][x]=null
      else
        d[y]={x}
      end
    end
    return set(d.values())
  end,
  function inv
    dict(self.items().map(|x,y| [y,x]))
  end
})

extend(Iterable,table{
  function uniq(*argv)
    if size(argv)==0
      return list(set(self))
    else
      p=argv[0]
      return list(self.dict(|x| [p(x),x]).values())
    end
  end,
  function cup
    self.reduce({},|x,y| x|y)
  end,
  function cap
    self.reduce(|x,y| x&y)
  end
})

extend(Function,table{
  function mul(g;f)
    |x| g(f(x))
  end
})

clamp = |x,a,b| min(max(x,a),b)

function affine_map(x1,x2,y1,y2)
  m = (y2-y1)/(x2-x1)
  return |x| y1+m*(x-x1)
end



